#version 330 core

// input common/clipping-space-textured.vertex.glsl

/////////////////////////////////////////////////////////////
//////////////////////// UNIFORMS ///////////////////////////
/////////////////////////////////////////////////////////////
uniform sampler2D u_colorBuffer;
uniform sampler2D u_motion;

/////////////////////////////////////////////////////////////
//////////////////////// VARYING ////////////////////////////
/////////////////////////////////////////////////////////////
in vec2 v_texCoord;

/////////////////////////////////////////////////////////////
/////////////////////////// OUT /////////////////////////////
/////////////////////////////////////////////////////////////
out vec4 o_fragColor;

/////////////////////////////////////////////////////////////
////////////////////////// MAIN /////////////////////////////
/////////////////////////////////////////////////////////////

void main() {
	vec2 motion = texture(u_motion, v_texCoord).xy;
    motion *= 0.5;

    vec2 pixelSize = 1.0 / textureSize(u_colorBuffer, 0);
	float maxDist = pixelSize.x * 40.0;
	if (length(motion) > maxDist) {
		motion *= maxDist / length(motion);
    }

	float steps = length(motion) / pixelSize.x * 0.5;
	steps = min(steps, 10.0);
	vec4 color = texture(u_colorBuffer, v_texCoord);
	float c = 1.0;
	for (float i = -steps + 0.5; i < steps - 0.5; i += 1.0)
	{
		float t = i / steps;
		float w = 1.0 - abs(t);
		color += texture(u_colorBuffer, v_texCoord + motion * t) * w;
		c += w;
	}
	color /= c;
	o_fragColor = color;
}
